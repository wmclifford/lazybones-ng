plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

archivesBaseName = "lazybones-gradle"
group = "uk.co.cacoethes"
version = "1.2.5-SNAPSHOT"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    // gradleApi() drags in the Groovy bundled with Gradle, so always a good idea
    // to specify localGroovy() too
    implementation localGroovy(), gradleApi()

    testImplementation "org.spockframework:spock-core:0.7-groovy-2.0", {
        exclude module: "groovy-all"
    }
    testImplementation "cglib:cglib-nodep:2.2.2"
}

// Disable publishing task(s) if no credentials are defined.
tasks.withType(PublishToMavenRepository) {
    onlyIf {
        project.hasProperty('repo.username') && project.hasProperty('repo.apiKey')
    }
}

publishing {
    publications {
        pluginLibrary(MavenPublication) {
            from components.java
        }
    }

    // Only include repositories for publication if credentials are defined.
    if (project.hasProperty('repo.username') && project.hasProperty('repo.apiKey')) {
        repositories {
            maven {
                credentials {
                    username project.findProperty('repo.username')
                    password project.findProperty('repo.apiKey')
                }
                name = 'lazybonesBintrayRepo'
                url = 'https://api.bintray.com/maven/pledbrook/plugins/lazybones-gradle'
            }
        }
    }
}

gradlePlugin {
    plugins {
        autoUpdate {
            id = 'uk.co.cacoethes.lazybones-templates'
            implementationClass = 'uk.co.cacoethes.gradle.LazybonesTemplatesPlugin'
        }
    }
}
